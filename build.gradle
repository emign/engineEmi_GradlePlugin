plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "java"
}


repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven { url = uri("https://dl.bintray.com/korlibs/korlibs") }

    mavenCentral()
    jcenter()
}

dependencies {
    api("com.soywiz.korlibs.korge.plugins:korge-gradle-plugin:$korgeVersion")

    api gradleApi()
    api localGroovy()
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}


group "$GROUP_ID"
version "$pluginVersion"

defaultTasks 'jar'

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
}



artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
    website = SITE_URL
    vcsUrl = VCS_URL
    tags = ['engineEmi', 'emig', 'engine', 'game engine', 'multiplatform', 'kotlin']

    plugins {
        engineEmi {
            id = "${GROUP_ID}.${ARTIFACT_ID}"
            displayName = ARTIFACT_ID
            description = 'Didactical Multiplatform Game Engine for Kotlin'
        }
    }
}

gradlePlugin {
    plugins {
        engineEmi {
            id = "${GROUP_ID}.${ARTIFACT_ID}"
            displayName = "${ARTIFACT_ID}"
            description = 'Didactical Multiplatform Game Engine for Kotlin'
            implementationClass = 'me.emig.engineEmi.gradle.EngineEmiGradlePlugin'
        }
    }
}


publishing {
    repositories {
        maven {
            credentials {
                username = BINTRAY_ORGANIZATION
                password = "$System.env.bintrayApiKey"

            }
            url = uri("https://api.bintray.com/maven/${BINTRAY_ORGANIZATION}/${BINTRAY_REPOSITORY}/${ARTIFACT_ID}/")
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "${GROUP_ID}.${ARTIFACT_ID}"
            artifactId = "${ARTIFACT_ID}"
            version = "$pluginVersion"


            pom {
                name = "${name}";
                description = "${description}";
                url = VCS_URL;
                scm {
                    url = VCS_URL;
                }
            }
        }
    }}

task release(type: Task) {
    //dependsOn("publish")
    dependsOn("publishAllPublicationsToMavenRepository")
    group="publishing"
    doLast {

        ((HttpURLConnection)new URL("https://bintray.com/api/v1/content/$BINTRAY_ORGANIZATION/$BINTRAY_REPOSITORY/$BINTRAY_REPOSITORY/$pluginVersion/publish").openConnection()).with({
            requestMethod = 'POST'
            doOutput = true

            setRequestProperty("Authorization", "Basic " + "emign:${System.env.bintrayApiKey}".bytes.encodeBase64().toString())

            outputStream.withPrintWriter({printWriter ->
                printWriter.write('{"discard": false, "publish_wait_for_secs": -1}')
            })
            System.out.println(inputStream.text)
        })
    }
}